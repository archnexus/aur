# Maintainer: OK100 <ok100 at lavabit dot com>
# Previous Maintainer: Val√®re Monseur <valere dot monseur at ymail dot com>

pkgname=compton-raedwulf-git
_gitname=compton
pkgver=2013.09.07.g00f9726
pkgrel=1
pkgdesc="X Compositor (a fork of xcompmgr-dana)"
arch=(i686 x86_64)
url="https://github.com/raedwulf/compton"
license=('MIT')
depends=('libgl' 'dbus' 'libxcomposite' 'libxdamage' 'libxrandr' 'pcre' 'libconfig')
makedepends=('git' 'asciidoc')
source=("git://github.com/raedwulf/compton.git#branch=develop")
md5sums=("SKIP")
conflicts=('compton' 'compton-git')
provides=('compton' 'compton-git')

pkgver() {
    cd "${srcdir}/${_gitname}"
    git log -1 --format="%cd.g%h" --date=short | sed 's/-/./g'
}

build() {
  cd "$srcdir/$_gitname"
  make PREFIX=/usr
  make docs
}

package() {
  cd "$srcdir/$_gitname"

  make PREFIX="$pkgdir/usr" install

  # install license
  install -D -m644 "LICENSE" "$pkgdir/usr/share/licenses/$_gitname/LICENSE"

  # example conf
  install -D -m644 "compton.sample.conf" "$pkgdir/etc/xdg/compton.conf.example"
}
